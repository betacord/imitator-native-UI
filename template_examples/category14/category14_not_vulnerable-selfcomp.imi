(************************************************************
 *                      IMITATOR MODEL
 *
 * Case study for side channel timing attack
 *
 * Description     : Encoding of a java program.
 * Correctness     : No information leakage
 * Source          : https://github.com/Apogee-Research/STAC/blob/master/Canonical_Examples/Source/Category2_not_vulnerable.java
 * Author          : https://github.com/Apogee-Research/STAC
 * Modeling        : Sun Jun, Étienne André, Li Jiaying
 * Input by        : Li Jiaying, Étienne André
 * License         : MIT License
 *
 * Created         : 2018/11/16
 * Last modified   : 2024/01/22
 *
 * IMITATOR version: 3.4
 ************************************************************)

var

(* Clocks *)
 	cl_1,
 	cl_2,
 	abs_clock,
		: clock;

(* Discrete *)
	flag_1,
	flag_2,
		: bool;

(* Parameters *)
	(* Global parameters necessary for the general encoding and our method *)
	epsilon,
 	abs_ptime,

 	(* Local parameters to encode symbolic data *)
 	userInput_length_1,
	xnonletters_1,
	first_mismatch_1,

 	userInput_length_2,
	xnonletters_2,
	first_mismatch_2,
		: parameter;

(* Constants *)
	passlen = 10,

		: constant;


(************************************************************)
  automaton pta_1
(************************************************************)
actions: setupserver_1, readx_1, verifycredentials_1;

loc l1: invariant cl_1 <= epsilon
	when cl_1 <= epsilon do {cl_1 := 0} sync setupserver_1 goto l2;

loc l2: invariant cl_1 <= epsilon
	when cl_1 <= epsilon do {cl_1 := 0} sync readx_1 goto l3;

loc l3: invariant cl_1 <= epsilon
	when cl_1 <= epsilon & userInput_length_1 > 10 do {cl_1 := 0} goto final1;
	when cl_1 <= epsilon & xnonletters_1 > 0 do {cl_1 := 0} goto final1;
	when cl_1 <= epsilon & userInput_length_1 <= 10 & xnonletters_1 = 0 do {cl_1 := 0} goto l4;

loc final1: invariant True

loc l4: invariant cl_1 <= epsilon
	when cl_1 <= epsilon do {cl_1 := 0} sync verifycredentials_1 goto l5;

loc l5: invariant cl_1 <= epsilon
	when cl_1 <= epsilon do {cl_1 := 0} goto l6;

(*  private static void checkChar(String candidate, int charNumber) { *)
(*** NOTE: only spends time for the first correct chars ***)
loc l6: invariant True
	(* Duplicate to ensure first_mismatch_1 is an integer *)
	(* Case incorrect password *)
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 0 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 1 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 2 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 3 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 4 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 5 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 6 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 7 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 8 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 9 do {cl_1 := 5000000 first_mismatch_1} goto l7;
	(* Case correct password *)
	when cl_1 = 5010000 first_mismatch_1 & first_mismatch_1 = 10 & not(flag_1) (*** NOTE: flag_1 = false is just to avoid the automatic removing of the flag by IMITATOR… ***) do {cl_1 := 5000000 first_mismatch_1, flag_1 := True} goto l7;

(*  private static void balance(int offset){ *)
(*** The time spent here is (candidate.length() - subsequentCorrect) * 5000000 ***)
loc l7: invariant cl_1 <= 5000000 passlen
	when cl_1 = 5000000 passlen do {cl_1 := 0} goto l8;

(* end process *)
loc l8: invariant cl_1 <= epsilon
	when cl_1 <= epsilon & abs_clock = abs_ptime do {cl_1 := 0} goto final2;

loc final2: invariant True

end (* pta *)




(************************************************************)
  automaton pta_2
(************************************************************)
actions: setupserver_2, readx_2, verifycredentials_2;

loc l1: invariant cl_2 <= epsilon
	when cl_2 <= epsilon do {cl_2 := 0} sync setupserver_2 goto l2;

loc l2: invariant cl_2 <= epsilon
	when cl_2 <= epsilon do {cl_2 := 0} sync readx_2 goto l3;

loc l3: invariant cl_2 <= epsilon
	when cl_2 <= epsilon & userInput_length_2 > 10 do {cl_2 := 0} goto final1;
	when cl_2 <= epsilon & xnonletters_2 > 0 do {cl_2 := 0} goto final1;
	when cl_2 <= epsilon & userInput_length_2 <= 10 & xnonletters_2 = 0 do {cl_2 := 0} goto l4;

loc final1: invariant True

loc l4: invariant cl_2 <= epsilon
	when cl_2 <= epsilon do {cl_2 := 0} sync verifycredentials_2 goto l5;

loc l5: invariant cl_2 <= epsilon
	when cl_2 <= epsilon do {cl_2 := 0} goto l6;

(*  private static void checkChar(String candidate, int charNumber) { *)
(*** NOTE: only spends time for the first correct chars ***)
loc l6: invariant True
	(* Duplicate to ensure first_mismatch_2 is an integer *)
	(* Case incorrect password *)
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 0 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 1 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 2 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 3 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 4 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 5 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 6 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 7 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 8 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 9 do {cl_2 := 5000000 first_mismatch_2} goto l7;
	(* Case correct password *)
	when cl_2 = 5010000 first_mismatch_2 & first_mismatch_2 = 10 & not(flag_2) (*** NOTE: flag_2 = false is just to avoid the automatic removing of the flag by IMITATOR… ***) do {cl_2 := 5000000 first_mismatch_2, flag_2 := True} goto l7;

(*  private static void balance(int offset){ *)
(*** The time spent here is (candidate.length() - subsequentCorrect) * 5000000 ***)
loc l7: invariant cl_2 <= 5000000 passlen
	when cl_2 = 5000000 passlen do {cl_2 := 0} goto l8;

(* end process *)
loc l8: invariant cl_2 <= epsilon
	when cl_2 <= epsilon & abs_clock = abs_ptime do {cl_2 := 0} goto final2;

loc final2: invariant True

end (* pta *)




(************************************************************)
(* Initial state *)
(************************************************************)
init := {

    discrete =
		(*------------------------------------------------------------*)
		(* Initial location *)
		(*------------------------------------------------------------*)
		loc[pta_1] := l1,
		loc[pta_2] := l1,

		(*------------------------------------------------------------*)
		(* Initial discrete variables assignments *)
		(*------------------------------------------------------------*)
		flag_1 := False,
		flag_2 := False,
    ;

    continuous =
		(*------------------------------------------------------------*)
		(* Initial clock constraints *)
		(*------------------------------------------------------------*)
		& cl_1 = 0
		& cl_2 = 0
		& abs_clock = 0

		(*------------------------------------------------------------*)
		(* Parameter constraints *)
		(*------------------------------------------------------------*)
		& abs_ptime >= 0
		& epsilon >= 0
		(* Value to compensate the noise between 5010000 and 5000000*)
		& epsilon <= 10000

		& userInput_length_1 >= 0
		& xnonletters_1 >= 0
		& 0 <= first_mismatch_1 & first_mismatch_1 <= userInput_length_1

		& userInput_length_2 >= 0
		& xnonletters_2 >= 0
		& 0 <= first_mismatch_2 & first_mismatch_2 <= userInput_length_2

	;
}


(************************************************************)
(* The end *)
(************************************************************)
end
