(*******************************************************************************
 *                                IMITATOR MODEL                               
 * 
 * Title            : Pipeline_KP12_2_3
 * Description      : The network presented in Figure 2 models the system consisting of the Producer feeding the Consumer with data sent through a sequence of nodes with additional processing capabilities. The model is scalable with respect to the number n of the processing nodes and the length m of each processing node and it contains three lower (a,c,e) and three upper (b,d,f) parameters. Note: the clocks were renamed (w.r.t. to [KP12]) for sake of simplicity.
 * Correctness      : φ2 = ConsWaiting ∧ ProdReady ∧ xtotal ≥ 5
 * Scalable         : yes
 * Generated        : 
 * Categories       : Academic ; ProdCons
 * Source           : SMT-based Parameter Synthesis for L/U Automata, Michał Knapik and Wojciech Penczek
 * bibkey           : KP12
 * Author           : Michał Knapik and Wojciech Penczek
 * Modeling         : Michał Knapik and Wojciech Penczek
 * Input by         : Étienne André
 * License          : 
 * 
 * Created          : 2015/09/18
 * Last modified    : 2021/08/30
 * Model version    : 
 * 
 * IMITATOR version : 3.1
 ******************************************************************************)


(* Etienne: each automaton is parameterized by a clock, 3 local actions and 2 shared actions. The models are ALSO parameterized in the number of computations (?) of each node; this could easily be made with a loop of n computations, where n is a new integer parameter of the template. And we need a LOCAL variable in addition, to be able to count up to max. *)

var

(* Clocks *)
 	xprod, xnode1, xnode2, xcons,
		xtotal,
		: clock;

(* Parameters *)
	a, b, c, d, e, f
		: parameter;

template Node(NodeProcess1 : action, NodeProcess2 : action, NodeProcess3 : action, Feed : action, Feed_ : action, xnode : clock)

actions: NodeProcess1, NodeProcess2, NodeProcess3, Feed, Feed_;

loc nodeReady: invariant xnode <= d
	when xnode >= c sync Feed do {xnode := 0} goto intermediate1;

loc intermediate1: invariant xnode <= f
	when xnode >= e sync NodeProcess1 do {xnode := 0} goto intermediate2;

loc intermediate2: invariant xnode <= f
	when xnode >= e sync NodeProcess2 do {xnode := 0} goto intermediate3;

loc intermediate3: invariant xnode <= f
	when xnode >= e sync NodeProcess3 do {xnode := 0} goto send;

loc send: invariant xnode <= d
	when xnode >= c sync Feed_ do {xnode := 0} goto nodeReady;

end


(************************************************************)
  automaton Producer
(************************************************************)
actions: ProdReset, Feed2;

loc prodReady: invariant xprod <= d
	when xprod >= c sync Feed2 do {xprod := 0} goto prodWaiting;

loc prodWaiting: invariant xprod <= b
	when xprod >= a sync ProdReset do {xprod := 0} goto prodReady;

end (* Producer *)



(************************************************************)
  automaton Consumer
(************************************************************)
actions: ConsReset, Feed4;

loc consReady: invariant xcons <= d
	when xcons >= c sync Feed4 do {xcons := 0} goto consWaiting;

loc consWaiting: invariant xcons <= b
	when xcons >= a sync ConsReset do {xcons := 0} goto consReady;

end (* Consumer *)

(************************************************************)
  automaton observer
(************************************************************)

(*** NOTE: as of version 2.10, IMITATOR cannot use clocks in the property; therefore we use an observer to encode the x >= 5 part of the property ***)
loc waiting: invariant xtotal <= 5
	when xtotal = 5 goto finished;

loc finished: invariant True

end (* observer *)


instantiate Node1 := Node(Node1Process1, Node1Process2, Node1Process3, Feed2, Feed3, xnode1);
instantiate Node2 := Node(Node2Process1, Node2Process2, Node2Process3, Feed3, Feed4, xnode2);

(************************************************************)
(* Initial state *)
(************************************************************)

init := {
  discrete =
  	(*------------------------------------------------------------*)
  	(* Initial location *)
  	(*------------------------------------------------------------*)
  	loc[Producer]	:= prodReady,
  	loc[Consumer]	:= consReady,
  	loc[Node1]	  := nodeReady,
  	loc[Node2]	  := nodeReady,
  	loc[observer]	:= waiting,
  ;

  continuous =
  	(*------------------------------------------------------------*)
  	(* Initial clock constraints *)
  	(*------------------------------------------------------------*)
  	& xprod = 0
  	& xnode1 = 0
  	& xnode2 = 0
  	& xcons = 0
  	& xtotal = 0

  	(*------------------------------------------------------------*)
  	(* Parameter constraints *)
  	(*------------------------------------------------------------*)
  	& a >= 0
  	& b >= 0
  	& c >= 0
  	& d >= 0
  	& e >= 0
  	& f >= 0
  ;
}


(************************************************************)
(* The end *)
(************************************************************)
end
